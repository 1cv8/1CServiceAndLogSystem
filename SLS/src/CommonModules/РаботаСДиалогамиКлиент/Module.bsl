
Процедура ЗаписатьДокументВФормеПриНеобходимости(ОповещениеПриЗавершении, Форма, ТребуетсяПроведение) Экспорт
	
	ТекстВопроса = "";
	Если Форма.Объект.Ссылка.Пустая() Или Не Форма.Объект.Проведен И ТребуетсяПроведение Тогда
		
		Если ТребуетсяПроведение Тогда
			ТекстВопроса = НСтр("ru = 'Документ не проведен. Провести?'");
		Иначе
			ТекстВопроса = НСтр("ru = 'Документ не записан. Записать?'");
		КонецЕсли;
		
	ИначеЕсли Форма.Модифицированность Тогда
		
		Если ТребуетсяПроведение Или Форма.Объект.Проведен Тогда
			ТекстВопроса = НСтр("ru = 'Документ был изменен. Провести?'");
		Иначе
			ТекстВопроса = НСтр("ru = 'Документ был изменен. Записать?'");
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстВопроса) Тогда
		
		Контекст = Новый Структура;
		Контекст.Вставить("Форма", Форма);
		Контекст.Вставить("ТребуетсяПроведение", ТребуетсяПроведение);
		Контекст.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
		Обработчик = Новый ОписаниеОповещения("ЗаписатьДокументВФормеПриНеобходимости_ПослеПодтверждения", ОбщегоНазначенияКлиент, Контекст);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьДокументВФормеПриНеобходимости_ПослеПодтверждения(Ответ, Контекст) Экспорт
	
	ЗаписаноУспешно = Ложь;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Если Контекст.ТребуетсяПроведение Тогда
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
		Иначе
			РежимЗаписи = РежимЗаписиДокумента.Запись;
		КонецЕсли;
		
		ЗаписаноУспешно = Контекст.Форма.Записать(Новый Структура("РежимЗаписи", РежимЗаписи));
		
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Контекст.ОповещениеПриЗавершении, ЗаписаноУспешно);
	
КонецПроцедуры

Процедура ЗаписатьСправочникВФормеПриНеобходимости(ОповещениеПриЗавершении, Форма, Знач НазваниеЭлементаСправочника = "") Экспорт
	
	Если НЕ ЗначениеЗаполнено(НазваниеЭлементаСправочника) Тогда
		НазваниеЭлементаСправочника = "Элемент справочника";
	КонецЕсли;
	
	ТекстВопроса = "";
	Если Форма.Объект.Ссылка.Пустая() Тогда
		ТекстВопроса = НСтр("ru = '" + НазваниеЭлементаСправочника + " не записан. Записать?'");
	ИначеЕсли Форма.Модифицированность Тогда
		ТекстВопроса = НСтр("ru = '" + НазваниеЭлементаСправочника + " был изменен. Записать?'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстВопроса) Тогда
		
		Контекст = Новый Структура;
		Контекст.Вставить("Форма", Форма);
		Контекст.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
		Обработчик = Новый ОписаниеОповещения("ЗаписатьСправочникВФормеПриНеобходимости_ПослеПодтверждения", ОбщегоНазначенияКлиент, Контекст);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьСправочникВФормеПриНеобходимости_ПослеПодтверждения(Ответ, Контекст) Экспорт
	
	ЗаписаноУспешно = Ложь;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаписаноУспешно = Контекст.Форма.Записать();
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Контекст.ОповещениеПриЗавершении, ЗаписаноУспешно);
	
КонецПроцедуры

