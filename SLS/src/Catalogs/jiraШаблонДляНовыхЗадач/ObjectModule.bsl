
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПерезаполнитьПараметрыПоШаблону(Отказ);
	
КонецПроцедуры

Процедура ПерезаполнитьПараметрыПоШаблону(Отказ)
	
	Для Каждого СтрокаПараметра ИЗ ПараметрыШаблона Цикл
		СтрокаПараметра.Используется = Ложь;
	КонецЦикла;
	
	НачПоз = 0;
	КонПоз = 0;
	
	НачПоз = СтрНайти(ШаблонJSON, "%");
	Пока НачПоз > 0 Цикл
		
		КонПоз = СтрНайти(ШаблонJSON, "%",, НачПоз+1);
		Если КонПоз = 0 Тогда
			Отказ = Истина;
			Сообщить("Нечётное количество символов ""%"" в шаблоне");
			Возврат;
		КонецЕсли;
		
		ИмяВШаблоне = Сред(ШаблонJSON, НачПоз+1, КонПоз - НачПоз-1);
		Если СтрЧислоСтрок(ИмяВШаблоне) > 1 Тогда
			Отказ = Истина;
			Сообщить("ИмяПараметра не может содержать переносы строк! (" + НачПоз + " симв.)");
			Возврат;
		КонецЕсли;
		
		Если СтрДлина(ИмяВШаблоне) > 150 Тогда
			Отказ = Истина;
			Сообщить("ИмяПараметра не может быть больше 150 символов: """ + ИмяВШаблоне + """");
			Возврат;
		КонецЕсли;
		
		СтрокаПараметра = ПараметрыШаблона.Найти(ИмяВШаблоне, "СтрокаЗамены");
		Если СтрокаПараметра = Неопределено Тогда
			СтрокаПараметра = ПараметрыШаблона.Добавить();
			СтрокаПараметра.СтрокаЗамены = ИмяВШаблоне;
		КонецЕсли;
		
		СтрокаПараметра.Используется = Истина;
		
		НачПоз = СтрНайти(ШаблонJSON, "%",, КонПоз+1);
		
	КонецЦикла;
	
КонецПроцедуры


#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли