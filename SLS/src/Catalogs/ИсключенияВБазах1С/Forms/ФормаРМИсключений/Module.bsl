
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.База1С.СписокВыбора.ЗагрузитьЗначения(ЖурналИсключений.ОрганизацииИсключений());
	
	Если НЕ ЗначениеЗаполнено(База1С) Тогда
		База1С = Константы.База1СДляПросмотраИсключенийПоУмолчанию.Получить();
	КонецЕсли;
	
	Элементы.Решение.СписокВыбора.Очистить();
	Элементы.Решение.СписокВыбора.Добавить(Перечисления.ВариантыРешенийПоИсключениям.ПустаяСсылка(), "Новые");
	Элементы.Решение.СписокВыбора.Добавить(Перечисления.ВариантыРешенийПоИсключениям.Исправлено);
	Элементы.Решение.СписокВыбора.Добавить(Перечисления.ВариантыРешенийПоИсключениям.Отложено);
	Элементы.Решение.СписокВыбора.Добавить(Перечисления.ВариантыРешенийПоИсключениям.Игнорировать, "Игнор");
	
	Элементы.СписокИсключенийИгнорировать.Видимость = Пользователи.РолиДоступны("ПравоПереводитьИсключенияВСтатусИгнорировать");
	
	ТолькоМои = Истина;
	
	ДатаДо = КонецДня(ТекущаяДата()) + 1;
	ДатаДо = ДатаДо + 9*60*60;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокИсключений,
		"База1С",
		База1С);
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		СписокИсключений,
		"Решение",
		Решение);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		СписокИсключений,
		"Ответственный",
		ПользователиКлиентСервер.АвторизованныйПользователь(),
		,,
		ТолькоМои);

КонецПроцедуры

&НаКлиенте
Процедура База1СПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокИсключений,
		"База1С",
		База1С);
	
КонецПроцедуры

&НаКлиенте
Процедура РешениеПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		СписокИсключений,
		"Решение",
		Решение);
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоМоиПриИзменении(Элемент)

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		СписокИсключений,
		"Ответственный",
		ПользователиКлиентСервер.АвторизованныйПользователь(),
		,,
		ТолькоМои);

КонецПроцедуры


&НаСервере
Процедура РешитьНаСервере(МассивИсключений)
	
	Для Каждого ИсключениеСсылка ИЗ МассивИсключений Цикл
		
		ЖурналИсключений.ОтметитьИсправлениеИсключения(База1С, ИсключениеСсылка, ДатаДо);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Решить(Команда)
	
	МассивИсключений = ВыделенныеСтроки();
	Если МассивИсключений.Количество() > 0 Тогда
		РешитьНаСервере(МассивИсключений);
	КонецЕсли;
	
	Элементы.СписокИсключений.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ИгнорироватьНаСервере(МассивИсключений)
	
	Для Каждого ИсключениеСсылка ИЗ МассивИсключений Цикл
		
		ЖурналИсключений.УстановитьИгнорИсключения(База1С, ИсключениеСсылка);
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура Игнорировать(Команда)
	
	МассивИсключений = ВыделенныеСтроки();
	Если МассивИсключений.Количество() > 0 Тогда
		ИгнорироватьНаСервере(МассивИсключений);
	КонецЕсли;
	
	Элементы.СписокИсключений.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ОтложитьНаСервере(МассивИсключений)
	
	Для Каждого ИсключениеСсылка ИЗ МассивИсключений Цикл
		
		ЖурналИсключений.ОтложитьОбработкуИсключения(База1С, ИсключениеСсылка, ДатаДо);
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура Отложить(Команда)
	
	МассивИсключений = ВыделенныеСтроки();
	Если МассивИсключений.Количество() > 0 Тогда
		ОтложитьНаСервере(МассивИсключений);
	КонецЕсли;
	
	Элементы.СписокИсключений.Обновить();
	
КонецПроцедуры

&НаКлиенте
Функция ВыделенныеСтроки()
	
	МассивСтрок = Новый Массив;
	Для каждого ВыбраннаяСтрока Из Элементы.СписокИсключений.ВыделенныеСтроки Цикл
		МассивСтрок.Добавить(ВыбраннаяСтрока);
	КонецЦикла;
	
	Возврат МассивСтрок;
	
КонецФункции

&НаКлиенте
Процедура ДатаДоПриИзменении(Элемент)

	ДатаДоМакс = КонецДня(ТекущаяДата()) + 1;
	ДатаДоМакс = ДатаДоМакс + 7*24*60*60;
	
	ДатаДоМин = КонецДня(ТекущаяДата()) + 1;
	ДатаДоМин = ДатаДоМин + 2*60*60;

	ДатаДо = Мин(ДатаДо, ДатаДоМакс);
	ДатаДо = Макс(ДатаДо, ДатаДоМин);

КонецПроцедуры

&НаСервере
Процедура ВернутьВРаботуНаСервере(МассивИсключений)

	Для Каждого ИсключениеСсылка ИЗ МассивИсключений Цикл
		
		ЖурналИсключений.ВернутьВРаботуИсключение(База1С, ИсключениеСсылка);
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ВернутьВРаботу(Команда)

	МассивИсключений = ВыделенныеСтроки();
	Если МассивИсключений.Количество() > 0 Тогда
		ВернутьВРаботуНаСервере(МассивИсключений);
	КонецЕсли;
	
	Элементы.СписокИсключений.Обновить();

КонецПроцедуры


