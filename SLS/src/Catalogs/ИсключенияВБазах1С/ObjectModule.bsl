
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.ПередЗаписьюСправочника(ЭтотОбъект, "Ответственный, ГруппаРассылки, Категория");

	Хэш =		ЖурналИсключений.ХэшПоТекстамИсключений(ИмяСобытия, ТекстИсключения);
	ХэшШаблон =	ЖурналИсключений.ХэшПоТекстамИсключений(ИмяСобытия, ТекстИсключенияШаблон);
	
	Если ЗначениеЗаполнено(ИмяСобытия) И ЗначениеЗаполнено(ТекстИсключения) Тогда
		НаименованиеСобытия = ИмяСобытия + ": " + ТекстИсключения;
	ИначеЕсли ЗначениеЗаполнено(ИмяСобытия) Тогда
		НаименованиеСобытия = ИмяСобытия;
	Иначе
		НаименованиеСобытия = ТекстИсключения;
	КонецЕсли;
	
	НаименованиеСобытия = СтрЗаменить(НаименованиеСобытия, Символы.ПС, " ");
	НаименованиеСобытия = СтрЗаменить(НаименованиеСобытия, Символы.Таб, " ");
	НаименованиеСобытия = СтрЗаменить(НаименованиеСобытия, "     ", " ");
	НаименованиеСобытия = СтрЗаменить(НаименованиеСобытия, "    ", " ");
	НаименованиеСобытия = СтрЗаменить(НаименованиеСобытия, "   ", " ");
	НаименованиеСобытия = СтрЗаменить(НаименованиеСобытия, "  ", " ");
	НаименованиеСобытия = СтрЗаменить(НаименованиеСобытия, "  ", " ");
	
	Наименование = НаименованиеСобытия;

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСсылки = ДополнительныеСвойства.ДанныеСсылки;
	Если
		(
			ДополнительныеСвойства.ЭтоНовый
			И(
				ЗначениеЗаполнено(Ответственный)
				ИЛИ ЗначениеЗаполнено(ГруппаРассылки)
				ИЛИ ЗначениеЗаполнено(Категория)
			)
		)
		ИЛИ Ответственный <> ДанныеСсылки.Ответственный
		ИЛИ ГруппаРассылки <> ДанныеСсылки.ГруппаРассылки
		ИЛИ Категория <> ДанныеСсылки.Категория
		Тогда
		
		ЖурналИсключений.ЗаписатьРеквизитыВИсторию(, Ссылка, Новый Структура("Ответственный,ГруппаРассылки,Категория", Ответственный,ГруппаРассылки,Категория));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли