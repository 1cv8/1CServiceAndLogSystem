
Функция ADDPOST(Запрос)
	
	Попытка
		
		ID = СокрЛП(Запрос.ПараметрыURL["ID"]);
		База1С = ЖурналИсключенийПовтИсп.База1СПоID(ID);
		Если НЕ ЗначениеЗаполнено(База1С) Тогда
			ВызватьИсключение ("Не найдена база с идентификатором: " + ID);
		КонецЕсли;
		
		МассивСообщений = Неопределено;
		ТелоСтрока = Запрос.ПолучитьТелоКакСтроку("UTF-8");
		Если НЕ ПустаяСтрока(ТелоСтрока) Тогда
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.УстановитьСтроку(ТелоСтрока);
			МассивСообщений = ПрочитатьJSON(ЧтениеJSON, Истина);
			ЧтениеJSON.Закрыть();
		КонецЕсли;
		
		Если МассивСообщений = Неопределено Тогда
			ВызватьИсключение ("Не удалось получить тело сообщения");
		КонецЕсли;
		
		Отказ = Ложь;
		СообщениеОбОшибке = "";
		ЖурналИсключений.ЗагрузитьМассивСообщенийЖРБазы(База1С, МассивСообщений, Отказ, СообщениеОбОшибке);
		
	Исключение
		
		ИнфоОбОшибке = ИнформацияОбОшибке();
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнфоОбОшибке);
		
		Отказ = Истина;
		
	КонецПопытки;
	
	Если Отказ ИЛИ ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
		
		Ответ = Новый HTTPСервисОтвет(500);
		Ответ.Заголовки.Вставить("Content-Type", "text/html; charset=UTF-8");
		Ответ.УстановитьТелоИзСтроки(СообщениеОбОшибке);
		
		Возврат Ответ;
		
	КонецЕсли;
	
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.Заголовки.Вставить("Content-Type", "text/html; charset=UTF-8");
	Ответ.УстановитьТелоИзСтроки("OK");
	
	Возврат Ответ;
	
КонецФункции

Функция ТестGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.Заголовки.Вставить("Content-Type", "text/html; charset=UTF-8");
	Ответ.Заголовки.Вставить("Cache-Control", "no-cache");
	
	Ответ.УстановитьТелоИзСтроки("OK");
	
	Возврат Ответ;
	
КонецФункции
